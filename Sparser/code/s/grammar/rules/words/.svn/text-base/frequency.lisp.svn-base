;;; -*- Mode:LISP; Syntax:Common-Lisp; Package:SPARSER -*-
;;; copyright (c) 1990-1996  David D. McDonald  -- all rights reserved
;;; extensions copyright (c) 2010 BBNT Solutions LLC. All Rights Reserved
;;; $Id$
;;; 
;;;     File:  "frequency"
;;;   Module:  "rules;words:"
;;;  Version:  0.4 August 2010

;; initiated 10/90
;; 3/21/92 Added capitalization information to the dummy words
;; (1/11/94 v2.3) modernized it all
;; 0.1 (6/12) switch'ified the runtime word-checking routine.
;; 0.2 (2/27/95) cleaned up loose ends.
;;     (6/26/96) moved from [analyzers;doc:] to [grammar;rules:words:]
;; 0.3 (6/9/10) Hacking *current-article* so we can to tf/idf analyses
;; 0.4 (6/19/10) Folding in Porter Stemmer. 6/30 tweaking that and the
;;     printers. 7/15/10 implementing tracking freq in different documents.
;;     7/23-25 folding in #<document> object. Refining ...8/

(in-package :sparser)

;;;------------
;;; parameters
;;;------------

(unless (boundp '*stem-words-for-frequency-counts*)
  (defparameter *stem-words-for-frequency-counts* t
    "If not nil, we apply the Porter Stemmer to each (non-function) word
     and use the stem it returns as the word that we count."))

(unless (boundp '*include-function-words-in-frequency-counts*)
  (defparameter *include-function-words-in-frequency-counts* t
    "All known words that have :function-word on their plist are
     usually lumped together under the pseudo-word that is bound
     to *function-word* below. Otherwise they will dominate any
     frequency count."))

(unless (boundp '*include-punctuation-in-frequency-counts*)
  (defparameter *include-punctuation-in-frequency-counts* t
    "Any word at a position whose capitalization is flagged as
     :punctuation will usually be lumped into the pseud-word
     *punctuation-word*"))

;;;-----------------
;;; state variables
;;;-----------------

(defvar *words-in-run* 0
  "Bumped with the recording of each word.  Intended to accumulate
   across multiple articles.")

(defvar *word-types* 0
  "Bumped with the recording of each NEW word.")

(defvar *word-types-at-start-of-article* 0
  "Keeps track of how many new words appear in successive articles
   when running over document streams")

(defparameter *word-frequency-classification* nil
  "Set by Establish-word-frequency-classification. Names the 
   pattern of categories that the words will be sorted into.")

(defparameter *word-count-buckets* nil
  "An alist that reflect a sorting of the word-frequency
   hashtable's data by count.  The first element in each item
   is the frequency, the second is the word count at that
   frequency, and the rest is the list of words.")

(defparameter *word-count-buckets-most-freq-highest* nil
  "The same list ordered from most frequent word to least.")

(defvar *documents-analyized* nil
  "A list of each document that has been analyzed for frequency
   in a given set (i.e. between initializations")


;;;----------------
;;; initialization
;;;----------------

(defun initialize-word-frequency-data ()
  (setq *words-in-run* 0
        *word-types* 0
        *word-types-at-start-of-article* 0
        *sorted-word-entries* nil
        *word-count-buckets* nil
        *word-count-buckets-most-freq-highest* nil
	*documents-analyized* nil) 
  (reset/over-all)) ;; (readout-frequency-table)


;;;------------------------------------
;;; the table for "over all" frequency
;;;------------------------------------

(defparameter *word-frequency-table*
              (make-hash-table :test #'eq
                               :size 1000
                               :rehash-size 500))


(defun frequency-table-entry/over-all (word)
  (gethash word *word-frequency-table*
           :no-entry  ;; the value returned if the word isn't in the table
           ))

(defun reset/over-all ()
  (clrhash *word-frequency-table*))


(defun frequency-table-to-list-of-symbols ()
  (let* ((words 
	  (loop for key being the hash-key in *word-frequency-table*
	     collect key))
	 (symbols (mapcar #'(lambda (w)
			      (intern (word-pname w)))
			  words)))
    (sort symbols #'alphabetize)))



;;;----------------------------
;;; driver - hook into Sparser
;;;----------------------------

(defun record-word-frequency (word position)
  ;; called as the body of Look-at-terminal
  (incf *words-in-run*)
  (let ((classification (classify-word-for-frequency word position)))
    (record-word-frequency/over-all word classification)))

#| An 'entry' is a cons whose car is the count, followed by
 an alist of (<article> . <per-article-count>).  Word objects
 are linked to their entries via the table *word-frequence-table*
|#

(defun record-word-frequency/over-all (word classification)
  (let ((entry
         (if classification ;; e.g. all function words lumped together
           (frequency-table-entry/over-all classification)
           (frequency-table-entry/over-all word)))
	(w (or classification
	       word)))
    (if (eq entry :no-entry)
      (make-initial-word-frequency-entry/over-all w)
      (increment-word-frequency-entry/over-all entry w))))



;; *current-article* is defined in drivers/sources/state.lisp which is 
;; loaded well before this file is. For our purposes it's bound in
;; one of the frequency drivers. The value is supposed to be an article
;; object, but haven't revived those yet (ddm 7/15/10)

(defun make-initial-word-frequency-entry/over-all (word)
  ;; called the first time a word is seen, i.e. when it isn't already
  ;; in the frequency table.
  (incf *word-types*)
  (setf (gethash word *word-frequency-table*)
        (cons 1
              (list (cons *current-article* 1)
                    ))))

(defun increment-word-frequency-entry/over-all (entry word)
  (incf (first entry))
  (let ((subentry-for-current-article 
	 (if *current-article*
	   (assq *current-article* (cdr entry))
	   (cadr entry)))) ;; only makes sense on a single-document run
    (when *current-article*
      (unless subentry-for-current-article
	(setq subentry-for-current-article
	      `(,*current-article* . 0))
	(rplacd entry (cons subentry-for-current-article
			    (cdr entry)))))
    (incf (cdr subentry-for-current-article))
    (when *current-article*
      (incf-word-count word *current-article*)) 
    subentry-for-current-article ))




(defun number-of-words-counted ()
  "All entries in all documents in the set that has been scanned"
  (hash-table-count *word-frequency-table*))



;;;-----------------------
;;; reporting the results
;;;-----------------------

(defun report-word-increment ()
  (let* ((last-time *word-types-at-start-of-article*)
         (difference (- *word-types*
                        last-time)))
    (format t "~&  ~A words added~%" difference)
    (setq *word-types-at-start-of-article* *word-types*)))


(defvar *sorted-word-entries* nil)

(defun setup-word-frequency-data ()
  (let ((words-counted
         (readout-word-frequency-table-into-a-list)))
    (setq *sorted-word-entries*
          (sort-frequency-list words-counted))
    (length *sorted-word-entries*)))


(defun readout-frequency-table ()
  "Prime reporting routine if just looking all all the words
   and not comparing word frequencies across documents"
  (setup-word-frequency-data)
  (display-sorted-results *standard-output*
                          *sorted-word-entries*)
  '*sorted-word-entries*)


(defun display-sorted-results (&optional
                               (stream *standard-output*)
                               (list-of-entries *sorted-word-entries*))
  (format stream "~&~%~A words in a corpus of length ~A"
          (number-of-words-counted) *words-in-run*)
  (let ((frequency 0)
        (words-on-the-line 0))
    (dolist (entry list-of-entries)
      (when (not (= (cdr entry) frequency))
        ;; the frequency just changed
        (setq frequency (cdr entry)
              words-on-the-line 0)
        (format stream "~&~%Word with frequency ~A~%   "
		frequency))
      (princ-word (car entry) stream)
      (write-string "  " stream)
      (incf words-on-the-line)
      (when (= 5 words-on-the-line)
        (format stream "~%   ")
        (setq words-on-the-line 0)))
    (terpri stream)
    (terpri stream)))


;;--- subroutines for reporting

(defun readout-word-frequency-table-into-a-list ()
  "Returns a list of (,word . ,count)"
  (let ( accumulator )
    (maphash
     #'(lambda (word entry)
         (push (cons word (first entry)) ;; total count, not
               accumulator))             ;; per-document
     *word-frequency-table*)
    accumulator))


(defun sort-frequency-list (list-of-entries)
  "Sorts the output of readout-word-frequency-table-into-a-list 
   first by count and then alphabetically on the word."
  (sort list-of-entries
        #'(lambda (first second)
            (cond ((< (cdr first)
                      (cdr second))
                   t)  ;; the first goes earlier in the result
                  ((> (cdr first)
                      (cdr second))
                   nil)
                  ((string< (word-pname (car first))
                            (word-pname (car second)))
                   t)
                  ((string> (word-pname (car first))
                            (word-pname (car second)))
                   nil)))))



;;--- Another way to bucket and report the results

(defun word-frequency-profile (&optional
                               (list-of-entries *sorted-word-entries*))
  ;; Scans the global list of sorted (<word> . <count>) data and
  ;; sorts it into buckets. Sets *word-count-buckets* to the list
  ;; of buckets and returns the count.
  (let ((current-count 0)
        list-of-lists  accumulating-words  )
    (dolist (entry list-of-entries)
      (when (not (= current-count (cdr entry)))
        ;; close out the ongoing bucket and start a new one
        (if accumulating-words  ;; startup check
          (then
            (push `(,current-count
                    ,(length accumulating-words)
                    ,@accumulating-words )
                  list-of-lists)
            (setq accumulating-words nil
                  current-count (cdr entry)))
          (setq current-count 1)))
      (push (car entry) accumulating-words))

    ;; close out the last entry
    (push `(,current-count
            ,(length accumulating-words)
            ,@accumulating-words )
          list-of-lists)

    (setq *word-count-buckets* (nreverse list-of-lists))
    (length *word-count-buckets*)))



(defun display-word-frequency-profile (&optional
                                       (stream *standard-output*))
  (unless *word-count-buckets*
    (word-frequency-profile))
  (let ( frequency  count )
    (dolist (entry *word-count-buckets*)
      (setq frequency (first entry)
            count (second entry))
      (format stream "~&~A~4,2T~A~%" frequency count))))


(defun words-with-frequency# (n)
  ;; returns the whole entry, not just the word list
  (unless *word-count-buckets*
    (word-frequency-profile))
  (assoc n *word-count-buckets*))


(defun top-N-frequent-words (n &optional (stream *standard-output*))
  (unless *word-count-buckets*
    (word-frequency-profile))
  (unless *word-count-buckets-most-freq-highest*
    (setq *word-count-buckets-most-freq-highest*
          (reverse *word-count-buckets*)))
  (let ( entry )
    (dotimes (i n)
      (setq entry (nth i *word-count-buckets-most-freq-highest*))
      (format stream "~&~A~5,2T~A" (car entry) (cddr entry)))))


;;;------------------------------------------------------------
;;; Differential counts by article (baby steps towards tf/idf)
;;;------------------------------------------------------------

(defmethod unique-words ((d1 document) (d2 document))
  "What wards are in d1 that aren't in d2?"
  (let ( unique-to-d1
	(table1 (words-to-count d1))
	(table2 (words-to-count d2)))
    (maphash #'(lambda (word count)
		 (declare (ignore count))
		 (unless (gethash word table2)
		   (push word unique-to-d1)))
	     table1)
    unique-to-d1))
    






;;;---------------
;;; alphabetizing
;;;---------------
        
;; the sort function
(defun alphabetize-words (w1 w2)
  (let ((pname1 (word-pname w1))
        (pname2 (word-pname w2)))
    (string< pname1 pname2)))

(defun alphabetize-word-list (global-symbol)
  (let ((sorted-list
         (sort (symbol-value global-symbol)
               #'alphabetize-words)))
    (set global-symbol sorted-list)))
 


;;;----------------------------
;;; classifying un/known words
;;;----------------------------

(defparameter *capitalized-word*
  (define-dummy-word/expr 'capitalized-word
    :capitalization :initial-letter-capitalized))

(defparameter *number-word*
  (define-dummy-word/expr 'number-word))

(defparameter *function-word*
  (define-dummy-word/expr 'function-word))

(defparameter *punctuation-word*
  (define-dummy-word/expr 'punctuation-word))


(defun classify-word-for-frequency (word position)
  (declare (ignore word position))
  (break "No classifier has been picked for measuring word ~
          frequency.~%You have to make a call to~
          ~%  Establish-word-frequency-classification"))


(defun establish-word-frequency-classification (keyword function-name)
  (unless (fboundp function-name)
    (format t "~&~%Warning: the word frequency classification function~
            ~%  ~A  is not yet defined." function-name))
  (setf (symbol-function 'classify-word-for-frequency)
        (symbol-function function-name))
  (setq *word-frequency-classification* keyword))

#|
(establish-word-frequency-classification :standard 
                                         'standard-wf-classification)
(establish-word-frequency-classification :ignore-capitalization
                                         'wf-classification/ignore-caps)
|#

(defun wf-classification/ignore-caps (word position)
  (if (word-rules word)
    (wf-classification/ignore-caps/known word position)
    (let ((capitalization (pos-capitalization position))
	  (stem (word-pname word))) ;; for default when not stemming
      (when *stem-words-for-frequency-counts*
	(let ((pname (word-pname word)))
	  (unless (eq capitalization :lower-case)
	    (setq pname (string-downcase pname)))
	  (setq stem (apply-Porter-stemmer pname))
	  ;;/// restore final "e" ?
	  (unless (string-equal stem pname)
	    (record-original-from-stem stem pname))))
      (case capitalization
	(:digits *number-word*)
	(otherwise
	 (or (word-named stem)
	     (define-word/expr stem)))))))

(defun wf-classification/ignore-caps/known (word position)
  (if (member :function-word (word-plist word))
    (if *include-function-words-in-frequency-counts*
      word
      *function-word*)
    (ecase (pos-capitalization position)
      (:lower-case word) 
      (:punctuation
       (if *include-punctuation-in-frequency-counts*
         word
	 *punctuation-word*))
      (:digits *number-word*)
      ((or :initial-letter-capitalized
           :all-caps
           :mixed-case
           :single-capitalized-letter)
       word ))))


;; N.b. hasn't got the stemmer option in it
(defun standard-wf-classification (word position)
  (if (word-rules word)
    (standard-wf-classification/known word position)
    (ecase (pos-capitalization position)
      (:lower-case word)  ;;//morph. ?
      (:digits *number-word*)
      ((or :initial-letter-capitalized
           :all-caps
           :mixed-case
           :single-capitalized-letter)
       *capitalized-word*))))

(defun standard-wf-classification/known (word position)
  (if (member :function-word (word-plist word))
    *function-word*
    (ecase (pos-capitalization position)
      (:lower-case word) 
      (:punctuation
       *punctuation-word*)
      (:digits *number-word*)
      ((or :initial-letter-capitalized
           :all-caps
           :mixed-case
           :single-capitalized-letter)
       *capitalized-word*))))


;;--- Stemming

(defun apply-Porter-stemmer (lowercase-string)
  (user::stem lowercase-string))

(defvar *stems-to-original-word-string* (make-hash-table))

(defun unporter (stemmed-string)
  (gethash stemmed-string *stems-to-original-word-string*))

(defun record-original-from-stem (stemmed-string original)
  (pushnew original 
	   (gethash stemmed-string *stems-to-original-word-string*)))
  

;;;--------
;;; shells
;;;--------

;; (initialize-word-frequency-data)
;; (readout-frequency-table)

(defun f/wf (namestring)
;  (initialize-word-frequency-data)
  (word-frequency-setting)
  (let ((*current-article*
	 (find-or-make-document-object namestring :clear)))
    (declare (special *current-article*))
    (pushnew *current-article* *documents-analyized*)
    (analyze-text-from-file namestring)))




;;;-----------------------------------
;;; specific drivers for biology text
;;;-----------------------------------

(defvar *bio-texts*
  '("/Users/ddm/ws/nlp/corpus/Halar/extractedText/chap11.txt"
    "/Users/ddm/ws/nlp/corpus/Halar/extractedText/chap12.txt"
    "/Users/ddm/ws/nlp/corpus/Halar/extractedText/chap13.txt"))

;;--- workspace
#|
 (word-frequency-setting)
 (initialize-word-frequency-data)
 (f/wf "/Users/ddm/ws/nlp/corpus/Halar/extractedText/chapter12.txt")
 (f/wf "/Users/ddm/ws/nlp/corpus/Halar/extractedText/chapter13.txt")
 (f/wf "/Users/ddm/ws/nlp/corpus/Halar/extractedText/AURA.txt")
 
 (setq *stem-words-for-frequency-counts* nil)
 (setq *include-function-words-in-frequency-counts* nil)
 (setq *include-punctuation-in-frequency-counts* nil)

 (gethash (word-named "cell") *word-frequency-table*)
 (setq c13 (get-document 'chapter13))
 (unique-words c12 c13)
|#

